WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

file = _{
    SOI ~
    header ~
    body ~
    EOI
}

header = { header_declaration* }

header_declaration = { header_key ~ "=" ~ header_value }

header_key = { ( ASCII_ALPHA_UPPER | "_" )+ }

header_value = { string_outer | number }

body = { func_declaration* }

func_declaration = { func_signature ~ "=" ~ expr }

func_signature = ${ ident ~ "(" ~ func_params ~ ")" }

func_params = _{ ident? ~ (WHITESPACE* ~ "," ~ WHITESPACE* ~ ident)* }

expr = !{ prefix* ~ atom ~ postfix* ~ (infix ~ prefix* ~ atom ~ postfix* )* }

atom = _{ func_call | ident | number | "(" ~ expr ~ ")" }

ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

func_call = ${ ident ~ "(" ~ func_args ~ ")" }

func_args = _{ expr? ~ (WHITESPACE* ~ "," ~ WHITESPACE* ~ expr)* }

string_outer = _{ "\"" ~ string ~ "\"" }

string = { string_inner* }

string_inner = _{ !("\"") ~ ASCII }

number = @{ decimal | integer }

decimal = @{ integer ~ "." ~ ASCII_DIGIT+ }

integer = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }

infix = _{ add | sub | mul | div | pow | modulo }
    add = { "+" }
    sub = { "-" }
    mul = { "*" }
    div = { "/" }
    pow = { "^" }
    modulo = { "%" }

prefix = _{ neg }
    neg = { "-" }

postfix = _{ fac }
    fac = { "!" }
