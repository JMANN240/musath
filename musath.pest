WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

document = _{
    SOI ~
    header ~
    body ~
    EOI
}

header = { header_declaration* }
header_declaration = { header_key ~ "=" ~ header_value }
header_key = { ( ASCII_ALPHA_UPPER | "_" )+ }
header_value = { string_outer | number }

body = { function* }

integer = @{ ASCII_DIGIT+ }
decimal = @{ integer ~ "." ~ integer }
number = { decimal | integer }

identifier = { ( ASCII_ALPHA | "_" )+ }

string_outer = _{ "\"" ~ string ~ "\"" }
string = { string_inner* }
string_inner = _{ !("\"") ~ ASCII }

function = { function_signature ~ "=" ~ expression ~ ";" }
function_signature = ${ identifier ~ "(" ~ WHITESPACE* ~ identifier? ~ ( WHITESPACE* ~ "," ~ WHITESPACE* ~ identifier )* ~ WHITESPACE* ~ ")" }
function_call = ${ identifier ~ "(" ~ WHITESPACE* ~ expression? ~ ( WHITESPACE* ~ "," ~ WHITESPACE* ~ expression )* ~ WHITESPACE* ~ ")" }

expression = !{ remainder }
remainder = { term ~ ( rem ~ term )* }
term = { factor ~ ( ( add | sub ) ~ factor )* }
factor = { power ~ ( ( mul | div ) ~ power )* }
power = { unary ~ ( pow ~ unary )* }
unary = { neg ~ unary | primary }
primary = { number | function_call | identifier | "(" ~ expression ~ ")" }

add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
pow = { "^" }
rem = { "%" }
neg = { "-" }
